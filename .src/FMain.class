' Gambas class file

' Copyright (C) 2025 Murilo Gomes Julio
' SPDX-License-Identifier: GPL-2.0-only

' Site: https://www.mugomes.com.br

Public Sub mnuAtualizarDB_Click()
  
  FAtualizarDB.ShowDialog()
  
End

Public Sub mnuOpcoes_Click()
  
  FOpcoes.ShowDialog()
  
End

Public Sub btnAdicionar_Click()
  
  Dim sDialog As Integer = Message.Question(("Add file or folder?"), ("File"), ("Folder"), ("Cancel"))
  
  If sDialog = 1 Then 
    Dialog.Title = ("Select Files")
    Dialog.Path = MConfig.get("dialogo", User.Home)
    If Not Dialog.OpenFile(True) Then 
      For Each row As String In Dialog.Paths
        cvArquivos.Add(row, row)
      Next
      
      MConfig.set("dialogo", Dialog.Path)
    Endif 
  Else If sDialog = 2 Then
    Dialog.Title = ("Select Folder")
    Dialog.Path = MConfig.get("dialogo", User.Home)
    If Not Dialog.SelectDirectory() Then 
      cvArquivos.Add(Dialog.Path, Dialog.Path)
      MConfig.set("dialogo", Dialog.Path)
    Endif
  Endif
Catch 
  Message.Error(("File or folder already added!"))
  
End

Public Sub btnRemover_Click()
  
  If cvArquivos.Selection.Count > 0 Then 
    For Each row As String In cvArquivos.Selection
      cvArquivos.Remove(row)
    Next
  Endif 
  
End

Public Sub btnVerificar_Click()
  
  For Each a As String In cvArquivos.Keys
    If Not IsNull(a) Then
      FVerificar.sArquivos.Add(a)
    Endif 
    
  Next
  
  If FVerificar.sArquivos.Count > 0 Then 
    FVerificar.ShowDialog()
  Endif 
  
End

Public Sub Form_Open()
  
  If Exist("/usr/bin/clamscan") = False Then
    Message.Error(("<b>clamscan</b> was not found!"))
    Quit
  Endif
  
  If Exist("/usr/bin/pkexec") = False Then
    Message.Error(("<b>pkexec</b> was not found!"))
    Quit
  Endif
  
  cvArquivos.Columns.Count = 1
  cvArquivos.Columns[0].Text = ("File")
  cvArquivos.Header = True
  
End

Public Sub mnuSobre_Click()
  
  FAbout.Show()
  
End

Public Sub mnuCheckUpdate_Click()

  Shell "xdg-open \"https://www.mugomes.com.br/2025/07/miantivirus.html\""

End

Public Sub mnuApoie_Click()

  Shell "xdg-open \"https://apoia.se/softwareslivresparalinux\""

End
